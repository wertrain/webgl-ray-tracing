<!DOCTYPE html>
<html lang="ja">
  <head>
    <meta charset="utf-8">
    <title>WebGL Ray Tracing - 06</title>
    <link href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/css/bootstrap.min.css" rel="stylesheet" integrity="sha384-BVYiiSIFeK1dGmJRAkycuHAHRg32OmUcww7on3RYdg4Va+PmSTsz/K68vbdEjh4u" crossorigin="anonymous">
    <link href="../css/style.css" rel="stylesheet">
  </head>
  <body>
    <script id="fs" type="x-shader/x-fragment">
        precision mediump float;
        uniform float time;       // 経過時間
        uniform vec2  resolution; // 画面サイズ

        struct Ray {
            vec3 origin;    // 始点
            vec3 direction; // 方向
        };

        struct Sphere {
            float radius;
            vec3  position;
            vec3  color;
        };

        struct Floor {
            vec3 position;
            vec3 normal;
            vec3 color;
        };

        struct Intersection {
            int   hit;       // レイのヒット回数
            vec3  hitPoint;  // レイのヒット位置
            vec3  normal;    // 法線
            vec3  color;     // 色
            float distance;  // 距離
            vec3  rayDir;    // レイの方向
        };

        const vec3  LDR = vec3(0.577);  // 光源の強度
        const float EPS = 0.0001;       // 疑似イプシロン(本来の意味としては1.0+x!=1.0となる最小のx値)
        const int   MAX_REF = 4;        // 反射回数

        Sphere sphere[3];
        Floor floor;

        void intersectInit(inout Intersection I) {
            I.hit      = 0;
            I.hitPoint = vec3(0.0);
            I.normal   = vec3(0.0);
            I.color    = vec3(0.0);
            I.distance = 1.0e+30;
            I.rayDir   = vec3(0.0);
        }

        void intersectSphere(Ray R, Sphere S, inout Intersection I) {
            // 球からレイの始点方向へのベクトル
            vec3  a = R.origin - S.position;
            float b = dot(a, R.direction);
            float c = dot(a, a) - (S.radius * S.radius);
            float d = b * b - c;
            float t = -b - sqrt(d);
            if(d > 0.0 && t > EPS && t < I.distance) {
                I.hitPoint = R.origin + R.direction * t;
                I.normal = normalize(I.hitPoint - S.position);
                d = clamp(dot(LDR, I.normal), 0.1, 1.0);
                I.color = S.color * d;
                I.distance = t;
                I.hit++;
                I.rayDir = R.direction;
            }
        }

        void intersectFloor(Ray R, Floor F, inout Intersection I) {
            // 床位置と床法線の内積(-)
            float d = -dot(F.position, F.normal);
            // レイ方向と床法線の内積
            float v = dot(R.direction, F.normal);
            float t = -(dot(R.origin, F.normal) + d) / v;
            if(t > EPS && t < I.distance) {
                I.hitPoint = R.origin + R.direction * t;
                I.normal = F.normal;
                float d = clamp(dot(LDR, I.normal), 0.1, 1.0);
                // チェック模様を描く
                float m = mod(I.hitPoint.x, 2.0);
                float n = mod(I.hitPoint.z, 2.0);
                if((m > 1.0 && n > 1.0) || (m < 1.0 && n < 1.0)) {
                    d *= 0.5;
                }
                // 奥にあるものほど暗くしていく
                float f = 1.0 - min(abs(I.hitPoint.z), 25.0) * 0.04;
                I.color = F.color * d * f;
                I.distance = t;
                I.hit++;
                I.rayDir = R.direction;
            }
        }

        void intersectExec(Ray R, inout Intersection I) {
            intersectSphere(R, sphere[0], I);
            intersectSphere(R, sphere[1], I);
            intersectSphere(R, sphere[2], I);
            intersectFloor(R, floor, I);
        }

        void main(void) {
            // ピクセルの位置
            vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

            // レイを作成する
            Ray ray;
            ray.origin = vec3(0.0, 2.0, 6.0);
            // Z 値がマイナス方向 ＝ 奥側に向かうレイ
            ray.direction = normalize(vec3(p.x, p.y, -1.0));

            // 球を作成する
            sphere[0].radius = 1.0;
            sphere[0].position = vec3(0.0, 1.0, 0.0);
            sphere[0].color = vec3(0.0, 1.0, 0.0);
            sphere[1].radius = 1.0;
            sphere[1].position = vec3(-3.0, 1.0, 0.0);
            sphere[1].color = vec3(1.0, 0.0, 0.0);
            sphere[2].radius = 1.0;
            sphere[2].position = vec3(3.0, 1.0, 0.0);
            sphere[2].color = vec3(0.0, 0.0, 1.0);
            
            // 床を作成する
            floor.position = vec3(0.0, -1.0, 0.0);
            floor.normal = vec3(0.0, 1.0, 0.0);
            floor.color = vec3(1.0);

            // 判定オブジェクトを準備
            Intersection its;
            intersectInit(its);

            // レイの反射
            vec3 destColor = vec3(ray.direction.y);
            vec3 tempColor = vec3(1.0);
            Ray q;
            intersectExec(ray, its);
            // 反射を反映する
            if(its.hit > 0) {
                // レイが反射した位置の色を反映する
                // 一時領域に色を合成して保持
                destColor = its.color;
                tempColor *= its.color;
                // 反射回数分ループする
                for(int j = 1; j < MAX_REF; ++j) {
                    q.origin = its.hitPoint + its.normal * EPS;
                    // 入射角と法線方向から反射角度を計算する
                    q.direction = reflect(its.rayDir, its.normal);
                    // 計算した反射角を使用して再トレース
                    intersectExec(q, its);
                    if(its.hit > j) {
                        // レイが反射した位置の色を反映する
                        // 一時領域に色を合成して保持
                        destColor += tempColor * its.color;
                        tempColor *= its.color;
                    }
                }
            }
            gl_FragColor = vec4(destColor, 1.0);
        }
    </script>
    <div class="container">
      <div class="text-center">
        <canvas id="canvas"></canvas>
      </div>
      <pre id="editor" style="height: 400px"></pre>
    </div>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/ace/1.2.0/ace.js"></script>
    <script src="https://code.jquery.com/jquery-3.2.1.min.js" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" crossorigin="anonymous"></script>
    <script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/js/bootstrap.min.js" integrity="sha384-Tc5IQib027qvyjSMfHjOMaLkfuWVxZxUPnCJA7l2mCWNIpG9mGCD8wGNIcPD7Txa" crossorigin="anonymous"></script>
    <script src="../js/common/common.js"></script>
    <script src="../js/common/glutil.js"></script>
    <script src="../js/common/aceutil.js"></script>
    <script>
        RayTracing.Common.initialize();
    </script>
  </body>
</html>
